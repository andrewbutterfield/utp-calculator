\section{Predicates}\label{sec:Predicates}

In Fig. \ref{fig:pred-types} we show the Haskell declarations
of the datatypes to represent predicates.
\begin{figure}[tb]
\begin{verbatim}
data Pred s
  = T | F | PVar String | Equal (Expr s) (Expr s) | Atm (Expr s)
  | Comp String [Pred s] | PSub (Pred s) (Substn s)
\end{verbatim}
  \caption{Predicate Datatype (\texttt{CalcTypes.lhs})}
  \label{fig:pred-types}
\end{figure}

Similar to expressions we have basic values such as true (\texttt{T})
and false (\texttt{F}),
 with predicate-valued variables (\texttt{PVar}),
and composite predicates (\texttt{Comp}) which are the predicate equivalent
of \texttt{App} (see Sect. \ref{sec:Expressions}).
We also have two ways to turn expressions into predicates.
One (\texttt{Atm}) lifts an expression, which should be boolean-valued
into an (atomic) predicate,
while the other is an explicit representation (\texttt{Equal})
for expression equality.
We can also substitute over predicates (\texttt{PSub}).

In many ways,
we define our predicates of interest
in much the same was as done for expressions.
Basic logic features such as negation, conjunction, etc.,
are not built in,
but have to be implemented using \texttt{Comp}.
A collection of these are pre-defined as part of the calculator,
in the Haskell module \texttt{StdPredicates}.

There are a few ways in which the treatment of predicates
differ from expressions:
\begin{itemize}
  \item
    The simplifier and some of the infrastructure for handling
    laws treats \texttt{PVar} in a special way.
    It is possible to associate an \texttt{AlfEntry} in the dictionary
    with a \texttt{PVar}, so defining its alphabet.
    This can be useful when reasoning about atomic state-change
    actions which only depend on $s$ and $s'$.
    Such entries will be looked up when certain
    side-conditions are being checked.
  \item
    We distinguish between having a definition associated with a \texttt{Comp},
    and having a way to simplify one.
  \item
    Rendering predicates involves the pretty printer
    so the interface is more complex.
    We explain this below.
\end{itemize}



\subsection{Coding Atomic Semantics}

Formally, using our shorthand notations, we define atomic behaviour as:
\[
    \A(A) \defs ls(in) \land A \land ls'=ls\ominus(in,out)
\]
where $A$ is a predicate whose alphabet is restricted to $s$ and $s'$,
$\A(\_)$ lifts this to the full alphabet,
and $S\ominus(T,V)$ is notation from \cite{DBLP:conf/icfem/WoodcockH02}
that stands for $(S\setminus T)\cup V$.


\subsubsection{Coding a Definition}

We want to define a composite, called "A" (representing $\A$).
We  define a function that takes a single predicate argument
and applies $\A$ to it
\begin{code}
patm pr = Comp "A" [pr] -- we assume pr has only s, s' free
\end{code}
We can now code up its definition,
which takes a dictionary, and a list of its subcomponents
and returns a string/predicate pair,
interpreted in the same manner as the string/expression pair
returned by the expression simplifier.

One way to code this is as follows.
First define our variables and expressions,
because these get used in a variety of places.
\begin{code}
ls = Var "ls" ; ls' = Var "ls'"
inp = Var "in" -- 'in' is a Haskell keyword
out = Var "out"
lsinout = App "sswap" [ls,inp,out]
\end{code}
Here, \verb@"sswap"@ is our name for $\ominus$,
and note that Haskell identifiers can contain
the prime (\verb@'@) character.
We then define our atomic predicates ($ls(in)$ and $ls'=ls\ominus(in,out)$)
\begin{code}
lsin = Atm $ App "subset" [inp,ls]
ls'eqlsinout = Equal ls' lsinout
\end{code}
Finally we can define $\A(a)$ as their conjunction,
where \texttt{mkAnd} is a smart constructor for \verb@Comp "And"@,
defined in \texttt{StdPredicates.lhs},
that does some optimisations on the fly (e.g. \verb$mkAnd [pr] = pr$).
\begin{code}
defnAtomic d [a] = Just ("A",mkAnd [lsin,a,ls'eqlsinout],True)
\end{code}
Here \texttt{a} represents the single ($A$) argument we expect.

\subsubsection{Coding for Pretty Printing}

For rendering \texttt{Comp} predicates, we are going to generate an instance
of the pretty-printer type \texttt{PP}, using a dictionary
and list of sub-predicates,
with two additonal arguments:
one of type \texttt{MarkStyle} which describes how markings should be rendered,
and one of type \texttt{Int} which gives a precedence level.
The type signature is
\begin{verbatim}
SubCompPrint s -> Dict s -> Int -> [Pred s] -> PP
\end{verbatim}
The first argument is a function,
and it is a bit of Haskell higher-order programming designed
to make it easier to track predicate contexts in order
to facilitating the generation of highlighting.
The user does not need to write this,
as it is supplied as the first argument by the pretty-printing
infrastructure.
What the user should do is use it in any recursive call
to pretty-print a sub-predicate.
\begin{verbatim}
type SubCompPrint s
 = Int       -- precedence level for sub-component
   -> Int    -- sub-component number, starts at 1
   -> Pred s -- sub-component
   -> PP
\end{verbatim}
It takes two integer arguments to begin.
The first is the precedence level to be used to render the
sub-component,
while the secound should denote the position of the sub-component
in the sub-component list, counting from 1.

Such code, for example, would be like the following
to render our atomic construct:
\begin{code}
ppPAtm sCP d p [pr]
 = pplist [ ppa "A"
          , ppbracket "(" (sCP 0 1 pr) ")"]
\end{code}
The functions \texttt{pplist}, \texttt{ppa} and \texttt{ppbracket}
build instances of \texttt{PP} respectively,
that represent lists of \texttt{PP},
atomic strings,
and an occurence of \texttt{PP} surrounded by the designated brackets.
Note that the \texttt{SubCompPrint} argument (\texttt{sCP})
is applied to \texttt{pr},
with the precedence is set to zero as it is bracketed,
and the sub-component number set to one as \texttt{pr} is the first
(and only) sub-component.



We will show how the pretty-printing for
sequential composition  ($\lseq$) in UTCP is defined,
to illustrate the support for infix notation.
\begin{code}
ppPSeq sCP d  p [pr1,pr2]
 = paren p precPSeq
     $ ppopen  (pad ";;") [ sCP precPSeq 1 pr1
                          , sCP precPSeq 2 pr2 ]
\end{code}
Here \texttt{pad} puts spaces around its argument,
and so its user here is equivalent to \verb$ppa " ;; "$,
while \texttt{ppopen} uses its first argument as a
separator between all the elements of its second list argument.
The \texttt{paren} function takes two precedence values,
and a \texttt{PP} value, and puts parentheses around it if the first precedence
number is greater than the second.
The variable \texttt{precPSeq} is the precedence level of sequential composition,
here defined to be tighter than disjunction,
but looser than conjunction, as defined in module \texttt{StdPrecedences}.
Note once more, the use of \texttt{sCP}, and how the 2nd integer argument
corresponds to the position of the sub-predicate involved.

\subsubsection{The Predicate Entry}~

The dictionary entry for predicates has the following form:
\begin{verbatim}
 | PredEntry
   { pcansub :: [String]
   , pprint  :: SubCompPrint s -> Dict s -> Int -> [Pred s] -> PP
   , alfa :: [String]
   , pdefn   :: Rewrite s
   , prsimp  :: Rewrite s
   }
type Rewrite s = Dict s -> [Pred s] -> RWResult s
type RWResult s
 = Maybe ( String  -- rewrite justification
         , Pred s  -- result predicate
         , Bool )  -- True if top-level modified
\end{verbatim}
Fields \texttt{pcansub} and \texttt{prsimp} are the predicate analoges
of \texttt{ecansub} and \texttt{eval} in the expression entry.
Here \texttt{pprint} plays the same role as eprint,
but is oriented towards pretty printing.
The \texttt{alfa} component allows an specific alphabet to
be associated with a composite
---if empty then the dictionary alphabet entries apply.

The \texttt{pdefn} component, of the same type as \texttt{prsimp},
is used when the user invokes the Definition Expansion
command from the REPL.
The calculator searches top-down, left-right
    for the first \texttt{Comp} whose \texttt{pdefn} function
    returns a changed outcome.

A \texttt{RWResult} can be \texttt{Nothing},
in which case this definition expansion or simplifier
was unable to make any changes.
If it was able to change its target then it returns
\texttt{Just(reason,newPred,isTopLevel)}.
The string \texttt{reason} is used to display the justification for the
calculation step to the user.
The \texttt{isTopLevel} flag is a hint to the change highlighting facilities
of the pretty-printer infrastructure.


The dictionary entry for our atomic semantics is then:
\begin{code}
patmEntry=("A",PredEntry [] ppPAtm [] defnAtomic (pNoChg "A"))
\end{code}
The function \texttt{pNoChg} creates a simplifer that returns \texttt{Nothing}.
